/*
* Copyright (C) 2017 Clivern <http://clivern.com>
*
* Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
* in compliance with the License. You may obtain a copy of the License at
*
* http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software distributed under the License
* is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
* or implied. See the License for the specific language governing permissions and limitations under
* the License.
*/

plugins {
    id 'java-library'
    id 'maven'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.28.1'
}

archivesBaseName = "racter"
version = '1.0.5'

spotless {
    java {
        licenseHeaderFile '.license'
        importOrder 'java'
        googleJavaFormat('1.1').aosp()
        target 'src/**/*.java'
    }
}

// Update Pom file ./gradlew updatePom
task updatePom {
    doLast {
        pom {
            project {
                groupId 'com.clivern'
                artifactId 'racter'
                version '1.0.5'
                name 'Racter'
                description "A Java Framework for Building Bots on Facebook's Messenger Platform."
                url 'https://github.com/clivern/racter'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'Clivern'
                        name 'Ahmed'
                        email 'hello@clivern.com'
                        url 'http://clivern.com'
                        roles {
                            role 'Software Engineer'
                        }
                    }
                }
                scm {
                    url 'https://github.com/clivern/racter'
                    connection 'scm:git:git://github.com/clivern/racter.git'
                    developerConnection 'scm:git:git@github.com:clivern/racter.git'
                }
            }
        }.writeTo("racter.pom").writeTo("racter.xml")
    }
}

test {
    //we want display the following test events
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    testImplementation 'junit:junit:4.13'
    compile 'com.squareup.okhttp3:okhttp:4.6.0'
    compile 'org.json:json:20190722'
    compile 'org.tinylog:tinylog:1.3.6'
}
